plugins {
    id "java"
    id "application"
    id "idea"
    id "jacoco"
    id 'io.freefair.aspectj.post-compile-weaving' version '6.3.0'
}

repositories {
    mavenCentral()
}

dependencies {

    implementation "com.amazonaws:aws-lambda-java-core:1.2.1",
            "com.amazonaws:aws-lambda-java-events:3.11.0",
            'com.amazonaws:aws-java-sdk-dynamodb:1.12.167',
            project(":lib")

    aspect 'software.amazon.lambda:powertools-tracing:1.12.0'


    compileOnly 'org.projectlombok:lombok:1.18.22'
    annotationProcessor 'org.projectlombok:lombok:1.18.22'

    testImplementation 'org.junit.jupiter:junit-jupiter:5.8.2',
            'org.mockito:mockito-core:4.3.1',
            'org.mockito:mockito-junit-jupiter:4.3.1'
}

java {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
}

task buildZip(type: Zip) {
    from compileJava
    from processResources
    destinationDirectory = file("$rootDir/dist")
    into("lib") {
        from configurations.runtimeClasspath
    }
}

test {
    // Configures environment variable to avoid initialization of AWS X-Ray segments for each tests
    environment "LAMBDA_TASK_ROOT", "handler"
    useJUnitPlatform ()
    finalizedBy jacocoTestReport
}

jacocoTestReport {
    dependsOn test
    reports {
        xml.required.set(true)
    }
}

import org.gradle.api.JavaVersion
import org.gradle.api.tasks.bundling.Zip

plugins {
    id "java"
    id "application"
    id "idea"
    id "org.sonarqube" version "3.3"
    id "jacoco"
    id "com.diffplug.spotless" version "5.17.1"
}

repositories {
    mavenCentral()
}

dependencies {

    implementation "com.amazonaws:aws-lambda-java-core:1.2.1",
            "com.amazonaws:aws-lambda-java-events:3.10.0",
            "com.amazonaws:aws-java-sdk-dynamodb:1.12.118",
            "com.nimbusds:oauth2-oidc-sdk:9.3.1",
            "com.fasterxml.jackson.core:jackson-core:2.13.0",
            "com.fasterxml.jackson.core:jackson-databind:2.13.0",
            "com.fasterxml.jackson.core:jackson-annotations:2.13.0",
            "org.apache.httpcomponents:httpclient:4.5.13",
            "org.slf4j:slf4j-simple:1.7.32",
            "software.amazon.awssdk:dynamodb-enhanced:2.17.89"


    compileOnly 'org.projectlombok:lombok:1.18.22'
    annotationProcessor 'org.projectlombok:lombok:1.18.22'

    testImplementation 'org.junit.jupiter:junit-jupiter:5.8.1',
            "org.mockito:mockito-core:3.12.4",
            "org.mockito:mockito-junit-jupiter:4.1.0",
            "com.github.tomakehurst:wiremock-jre8:2.31.0"
}

test {
    useJUnitPlatform ()
}

java {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
}


task buildZip(type: Zip) {
    from compileJava
    from processResources
    into("lib") {
        from configurations.runtimeClasspath
    }
}

sonarqube {
    properties {
        property "sonar.projectKey", "alphagov_di-ipv-core-back"
        property "sonar.organization", "alphagov"
        property "sonar.host.url", "https://sonarcloud.io"
    }
}

jacocoTestReport {
    reports {
        xml.enabled true
    }
}
test.finalizedBy jacocoTestReport

spotless {
    java {
        target "**/*.java"
        googleJavaFormat("1.12.0").aosp()
        importOrder "", "javax", "java", "\\#"
    }
}

compileJava.dependsOn "spotlessCheck"

